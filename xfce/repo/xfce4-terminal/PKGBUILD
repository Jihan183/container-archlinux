# Maintainer: Chigozirim Chukwu <noblechuk5[at]web[dot]de>

pkgname='xfce4-terminal'
pkgver=1.0
pkgrel=1
pkgdesc=''
arch=(any)
url='https://gitlab.xfce.org/apps/xfce4-terminal'
license=(GPLv2)
groups=(xfce-test)
depends=('hicolor-icon-theme' 'libxfce4ui' 'xfconf' 'vte3>=0.46')
makedepends=('intltool' 'libxslt' 'xfce4-dev-tools')
optdepends=(
  'libutempter: Support for utmp/wtmp files'
)
provides=("$pkgname")
conflicts=("$pkgname")
options=()
source=("$pkgname::git+$url.git${MAIN_BRANCH:+#branch=$MAIN_BRANCH}")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe | sed -E "s:^$pkgname.::;s/^v//;s/^xfce-//;s/([^-]*-g)/r\1/;s/-/./g"
}

prepare() {
  cd "$pkgname"
  if [ -n "$DOWNLOAD_DATE" ]; then
    commit_sha="$(git rev-list -1 --before="$DOWNLOAD_DATE" --abbrev-commit --abbrev=14 HEAD)"
    if [ -n "$commit_sha" ]; then
      git checkout --quiet "$commit_sha"
    else
      echo "Can't switch to specific date $DOWNLOAD_DATE - leaving as is on 'master'"
    fi
  fi
}

build() {
  cd "$pkgname"
  ./autogen.sh \
    --prefix=/usr/ \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --disable-debug \
    --disable-static \
    --enable-maintainer-mode \
    --enable-gtk-doc
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="${pkgdir}" install
}
