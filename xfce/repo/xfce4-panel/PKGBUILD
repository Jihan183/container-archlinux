# Maintainer: Chigozirim Chukwu <noblechuk5[at]web[dot]de>

pkgname='xfce4-panel'
pkgver=1.0
pkgrel=1
pkgdesc='Panel for the Xfce desktop environment'
arch=(any)
url='https://gitlab.xfce.org/xfce/xfce4-panel'
license=(GPLv2)
groups=(xfce-test)
depends=('desktop-file-utils' 'exo' 'garcon' 'hicolor-icon-theme' 'libdbusmenu-gtk3' 'libwnck3' 'libxfce4ui' 'libxfce4util' 'xfconf')
makedepends=('git' 'gobject-introspection' 'gtk-doc' 'intltool' 'vala' 'xfce4-dev-tools')
optdepends=()
provides=("$pkgname")
conflicts=("$pkgname")
options=()
source=("$pkgname::git+$url.git${MAIN_BRANCH:+#branch=$MAIN_BRANCH}")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe | sed -E "s:^$pkgname.::;s/^v//;s/^xfce-//;s/([^-]*-g)/r\1/;s/-/./g"
}

prepare() {
  cd "$pkgname"
  if [ -n "$DOWNLOAD_DATE" ]; then
    commit_sha="$(git rev-list -1 --before="$DOWNLOAD_DATE" --abbrev-commit --abbrev=14 HEAD)"
    if [ -n "$commit_sha" ]; then
      git checkout --quiet "$commit_sha"
    else
      echo "Can't switch to specific date $DOWNLOAD_DATE - leaving as is on 'master'"
    fi
  fi
}

build() {
  cd "$pkgname"
  ./autogen.sh  \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --enable-gio-unix \
    --enable-gtk-doc \
    --disable-debug
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="${pkgdir}" install
}
