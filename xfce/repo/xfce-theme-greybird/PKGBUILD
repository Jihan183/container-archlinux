# Maintainer: Chigozirim Chukwu <noblechuk5[at]web[dot]de>

pkgname='xfce-theme-greybird'
pkgver=1.0
pkgrel=1
pkgdesc=''
arch=(any)
url='https://github.com/shimmerproject/Greybird'
license=(GPLv2)
groups=(xfce-build)
depends=('gtk-engine-murrine')
makedepends=('git' 'meson' 'sassc')
optdepends=()
provides=("$pkgname")
conflicts=("$pkgname")
options=()
source=("$pkgname::git+$url.git${MAIN_BRANCH:+#branch=$MAIN_BRANCH}")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe | sed -E "s:^$pkgname.::;s/^v//;s/^xfce-//;s/([^-]*-g)/r\1/;s/-/./g"
}

prepare() {
  cd "$pkgname"
  if [ -n "$DOWNLOAD_DATE" ]; then
    commit_sha="$(git rev-list -1 --before="$DOWNLOAD_DATE" --abbrev-commit --abbrev=14 HEAD)"
    if [ -n "$commit_sha" ]; then
      git checkout --quiet "$commit_sha"
    else
      echo "Can't switch to specific date $DOWNLOAD_DATE - leaving as is on 'master'"
    fi
  fi
}

build() {
  cd "$pkgname"
  arch-meson . build
  meson compile -C build
}

package() {
  cd "$pkgname"
  DESTDIR="$pkgdir" meson install -C build
}
