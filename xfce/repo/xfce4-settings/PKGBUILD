# Maintainer: Chigozirim Chukwu <noblechuk5[at]web[dot]de>

pkgname='xfce4-settings'
pkgver=1.0
pkgrel=1
pkgdesc=''
arch=(any)
url='https://gitlab.xfce.org/xfce/xfce4-settings'
license=(GPLv2)
groups=(xfce-test)
depends=('adwaita-icon-theme' 'exo' 'garcon' 'gnome-themes-extra' 'libxfce4ui' 'libxklavier' 'xfconf' 'fontconfig' 'libxrandr')
makedepends=('git' 'intltool' 'xfce4-dev-tools')
optdepends=(
  'colord: colord support'
  'libinput: libinput Xorg driver support'
  'libnotify: Notification support'
)
provides=("$pkgname")
conflicts=("$pkgname")
options=()
source=("$pkgname::git+$url.git${MAIN_BRANCH:+#branch=$MAIN_BRANCH}")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe | sed -E "s:^$pkgname.::;s/^v//;s/^xfce-//;s/([^-]*-g)/r\1/;s/-/./g"
}

prepare() {
  cd "$pkgname"
  if [ -n "$DOWNLOAD_DATE" ]; then
    commit_sha="$(git rev-list -1 --before="$DOWNLOAD_DATE" --abbrev-commit --abbrev=14 HEAD)"
    if [ -n "$commit_sha" ]; then
      git checkout --quiet "$commit_sha"
    else
      echo "Can't switch to specific date $DOWNLOAD_DATE - leaving as is on 'master'"
    fi
  fi
}

build() {
  cd "$pkgname"
  ./autogen.sh \
    --disable-systemd \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --disable-debug \
    --disable-static \
    --enable-maintainer-mode \
    --enable-xrandr \
    --enable-xcursor \
    --enable-libnotify \
    --enable-libxklavier \
    --enable-pluggable-dialogs \
    --enable-sound-settings \
    --enable-gio-unix \
    --enable-sound-settings \
    --enable-gio-unix
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="${pkgdir}" install
}
