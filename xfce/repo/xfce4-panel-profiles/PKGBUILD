# Maintainer: Chigozirim Chukwu <noblechuk5[at]web[dot]de>

pkgname='xfce4-panel-profiles'
pkgver=1.0
pkgrel=1
pkgdesc=''
arch=(any)
url='https://gitlab.xfce.org/apps/xfce4-panel-profiles'
license=(GPLv2)
groups=(xfce-test)
depends=('python-gobject' 'xfce4-panel')
makedepends=('git' 'intltool')
optdepends=()
provides=("$pkgname")
conflicts=("$pkgname")
options=()
source=("$pkgname::git+$url.git${MAIN_BRANCH:+#branch=$MAIN_BRANCH}")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe | sed -E "s:^$pkgname.::;s/^v//;s/^xfce-//;s/([^-]*-g)/r\1/;s/-/./g"
}

prepare() {
  cd "$pkgname"
  if [ -n "$DOWNLOAD_DATE" ]; then
    commit_sha="$(git rev-list -1 --before="$DOWNLOAD_DATE" --abbrev-commit --abbrev=14 HEAD)"
    if [ -n "$commit_sha" ]; then
      git checkout --quiet "$commit_sha"
    else
      echo "Can't switch to specific date $DOWNLOAD_DATE - leaving as is on 'master'"
    fi
  fi
}

build() {
  cd "$pkgname"
  ./configure  --prefix=/usr
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="${pkgdir}" install
}
