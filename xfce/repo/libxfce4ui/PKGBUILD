# Maintainer: Chigozirim Chukwu <noblechuk5[at]web[dot]de>

pkgname='libxfce4ui'
pkgver=1.0
pkgrel=1
pkgdesc=''
arch=(any)
url='https://gitlab.xfce.org/xfce/libxfce4ui'
license=(GPLv2)
groups=(xfce-build)
depends=('gtk3' 'hicolor-icon-theme' 'libgtop' 'libsm' 'libxfce4util' 'startup-notification' 'xfconf')
makedepends=('git' 'glade' 'gobject-introspection' 'gtk-doc' 'intltool' 'vala' 'xfce4-dev-tools')
optdepends=()
provides=("$pkgname")
conflicts=("$pkgname")
options=()
source=("$pkgname::git+$url.git${MAIN_BRANCH:+#branch=$MAIN_BRANCH}")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe | sed -E "s:^$pkgname.::;s/^v//;s/^xfce-//;s/([^-]*-g)/r\1/;s/-/./g"
}

prepare() {
  cd "$pkgname"
  if [ -n "$DOWNLOAD_DATE" ]; then
    commit_sha="$(git rev-list -1 --before="$DOWNLOAD_DATE" --abbrev-commit --abbrev=14 HEAD)"
    if [ -n "$commit_sha" ]; then
      git checkout --quiet "$commit_sha"
    else
      echo "Can't switch to specific date $DOWNLOAD_DATE - leaving as is on 'master'"
    fi
  fi
}

build() {
  cd "$pkgname"
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug \
    --enable-gtk-doc
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="${pkgdir}" install
}
