# Maintainer: Chigozirim Chukwu <noblechuk5[at]web[dot]de>

pkgname='xfce4-vala'
pkgver=1.0
pkgrel=1
pkgdesc='Vala bindings for the Xfce Desktop Environment'
arch=(any)
url='https://gitlab.xfce.org/bindings/xfce4-vala'
license=(GPLv2)
groups=(xfce-test)
depends=()
makedepends=('xfce4-dev-tools')
optdepends=()
provides=("$pkgname")
conflicts=("$pkgname")
options=(!libtool)
source=("$pkgname::git+$url.git${MAIN_BRANCH:+#branch=$MAIN_BRANCH}")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe | sed -E "s:^$pkgname.::;s/^v//;s/^xfce-//;s/([^-]*-g)/r\1/;s/-/./g"
}

prepare() {
  cd "$pkgname"
  if [ -n "$DOWNLOAD_DATE" ]; then
    commit_sha="$(git rev-list -1 --before="$DOWNLOAD_DATE" --abbrev-commit --abbrev=14 HEAD)"
    if [ -n "$commit_sha" ]; then
      git checkout --quiet "$commit_sha"
    else
      echo "Can't switch to specific date $DOWNLOAD_DATE - leaving as is on 'master'"
    fi
  fi
}

build() {
  cd "$pkgname"
  # The `--with-vala-api='0.52'` is set in accordance with the latest
  # version of vala currently available on arch
  ./autogen.sh \
    --disable-systemd \
    --prefix=/usr \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --enable-maintainer-mode \
    --with-vala-api='0.52'
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="${pkgdir}" install
}
