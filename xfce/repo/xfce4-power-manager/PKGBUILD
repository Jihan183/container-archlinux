# Maintainer: Chigozirim Chukwu <noblechuk5[at]web[dot]de>

pkgname='xfce4-power-manager'
pkgver=1.0
pkgrel=1
pkgdesc=''
arch=(any)
url='https://gitlab.xfce.org/xfce/xfce4-power-manager'
license=(GPLv2)
groups=(xfce-test)
depends=('hicolor-icon-theme' 'libnotify' 'libxfce4ui' 'upower' 'libxrandr' 'xfconf' 'gtk3' 'glib2')
makedepends=('git' 'intltool' 'xfce4-dev-tools' 'xfce4-panel')
optdepends=()
provides=("$pkgname")
conflicts=("$pkgname")
options=()
source=("$pkgname::git+$url.git${MAIN_BRANCH:+#branch=$MAIN_BRANCH}")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe | sed -E "s:^$pkgname.::;s/^v//;s/^xfce-//;s/([^-]*-g)/r\1/;s/-/./g"
}

prepare() {
  cd "$pkgname"
  if [ -n "$DOWNLOAD_DATE" ]; then
    commit_sha="$(git rev-list -1 --before="$DOWNLOAD_DATE" --abbrev-commit --abbrev=14 HEAD)"
    if [ -n "$commit_sha" ]; then
      git checkout --quiet "$commit_sha"
    else
      echo "Can't switch to specific date $DOWNLOAD_DATE - leaving as is on 'master'"
    fi
  fi
}

build() {
  cd "$pkgname"
  ./autogen.sh \
    --disable-systemd \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --disable-debug \
    --disable-static \
    --enable-maintainer-mode \
    --enable-polkit \
    --enable-panel-plugins \
    --disable-network-manager
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="${pkgdir}" install
}
